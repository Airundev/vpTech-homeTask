apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "com.vp.movies"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        buildConfigField "String", "API_KEY", localProperties['apiKey']
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'en' // Only English is used in the app
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        debugProguard {
            initWith buildTypes.debug

            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled true
            shrinkResources true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude("/META-INF/*.kotlin_module")
        exclude("**/kotlin/**")
        exclude("**/okhttp3/**") // This can be removed since we know the only domains we are connecting to
        exclude("**/*.txt")
        exclude("**/*.xml")
        exclude("**/*.properties")
    }
}

dependencies {
    implementation project(':features:list')
    implementation project(':features:detail')
    implementation project(':features:favorites')
    implementation project(':data')
    implementation project(':dagger.addons')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlin"

    //support
    implementation "androidx.appcompat:appcompat:$rootProject.supportLibraryVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayout"

    //dagger
    implementation "com.google.dagger:dagger:$rootProject.dagger"
    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android-support:$rootProject.dagger"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger"

    //room
    implementation "androidx.room:room-runtime:$rootProject.lifecycle"
    kapt "androidx.room:room-compiler:$rootProject.lifecycle"

    //api
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttp"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit"

    //test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
